plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

mixin {
    add sourceSets.main, 'krtscp.refmap.json'
    config 'krtscp.mixins.json'
    debug.export = true
}

group = 'me.kuart'
archivesBaseName = 'krtscp'

sourceSets {
    api
    main.compileClasspath += api.output
    datagen {
        compileClasspath += api.output
        compileClasspath += main.output
    }
}
configurations {
    apiImplementation.extendsFrom(minecraft)
    datagenImplementation.extendsFrom(minecraft)
}


java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'parchment', version: '2022.11.27-1.19.2'

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        all {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'krtscp'
            mods {
                krtscp {
                    source sourceSets.main
                    source sourceSets.api

                }
            }
        }
        
        client {}
        server {}
        gameTestServer {}

        data {
            workingDirectory project.file('run')
            args '--mod', 'krtscp', '--all',
                    '--output', file('src/generated/resources/'),
                    '--existing', file('src/main/resources/') 
            mods {
                krtscp {
                    source sourceSets.datagen
                }
            }

        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft 'net.minecraftforge:forge:1.19.2-43.3.0'
annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

final manifestAttributes = [
        'Specification-Title'   : 'krtscp',
        'Specification-Vendor'  : 'Kuart',
        'Specification-Version' : '1',
        'Implementation-Title'  : project.name,
        'Implementation-Version': project.jar.archiveVersion,
        'Implementation-Vendor' : 'Kuart'
]
manifestAttributes.put('MixinConfigs', 'krtscp.mixins.json')

jar {
    manifest.attributes(manifestAttributes)
    finalizedBy('reobfJar')
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
